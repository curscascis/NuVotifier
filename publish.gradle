// This file contains special build instructions specifically for the ParallelBlock CI in order to instruct it how to
// build the project. Usually, this shouldn't be interacted with by a human.

import com.mashape.unirest.http.*

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.mashape.unirest:unirest-java:1.4.9'
    }
}

subprojects {
    apply plugin: "maven-publish"
    publishing {
        repositories {
            maven {
                name 'parallelBlock'
                if (project.version.endsWith("-SNAPSHOT"))
                    url "$System.env.SNAPSHOT_REPO"
                else
                    url "$System.env.RELEASE_REPO"
                credentials {
                    username "$System.env.REPO_USERNAME"
                    password "$System.env.REPO_PASSWORD"
                }
            }
        }
        publications {
            versioned(MavenPublication) {
                from components.java
            }
        }
    }

    if (System.env.LATEST_BUILD) {
        task publishLatestToParallelBlockRaw {
            inputs.files(configurations.archives.artifacts.files)
            outputs.upToDateWhen {false}
            doLast {
                configurations.archives.artifacts.each {
                    def resp = Unirest.put("$System.env.RAW_UPLOAD_PATH/${it.name}.${it.extension}")
                            .basicAuth("$System.env.REPO_USERNAME", "$System.env.REPO_PASSWORD")
                            .body(it.file.bytes).asString()
                    assert resp.status >= 200 && resp.status <= 299
                }
            }
        }
    }
}
